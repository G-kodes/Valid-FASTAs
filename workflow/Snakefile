from snakemake.utils import min_version, validate
import os

min_version("6.0")


# DEFINE CONFIG FILE FOR SNAKEMAKE:
configfile: os.path.join("config", "config.json")


validate(config, os.path.join("..", "config", "config.schema.json"))

include: "rules/common.py"

ruleorder: CP > BGZIP > SEQUENCE_DICTIONARY

wildcard_constraints:
    path="^([A-Z]{0,1}:{1}[\\|\/]{1,2}){0,1}(.+[\\\/])",
    ext="(\.fa|\.fa\.gz|\.fa\.gz\.faidx|\.fa\.gz\.dict)$"


rule all:
    input:
        # GetFinalOutput,
        expand("results/{name}.{ext}", name=[item["Name"] for item in config["Data"]], ext=["fa.gz", "fa.gz.faidx", "fa.gz.dict"])


rule CP:
    """
    Copy any files that do not require compression or generation
    """
    log:
        "results/{filename}{ext}.log",
    input:
        GetInputFile,
    output:
        "results/{filename}{ext}",
    params:
        executable=config["Executables"]["cp"],
    conda:
        "envs/VALID-FASTAS.yaml"
    shell:
        """
        echo 'Copying {input} -> {output}'
        {params.executable} "{input}" "{output}"
        """


rule BGZIP:
    """
    Use BG-Zip compression to generate GATK-comaptible `.gz` file for either FASTA or VCF format uncompressed inputs
    """
    log:
        "results/{filename}.log",
    input:
        "results/{filename}.fa",
    output:
        "results/{filename}.fa.gz",
    params:
        executable=config["Executables"]["BGZip"],
    conda:
        "envs/VALID-FASTAS.yaml"
    shell:
        """
        echo 'BGZipping {input} -> {output}'
        {params.executable} < "{input}" > "{output}"
        """


rule SEQUENCE_DICTIONARY:
    """
    Generate a companion-file sequence dictionary `.dict` file for BG-Zipped FATSA format sequence file
    """
    log:
        "results/{filename}.log",
    input:
        "results/{filename}.fa.gz",
    output:
        "results/{filename}.fa.gz.dict",
    params:
        executable=config["Executables"]["GATK"],
    conda:
        "envs/VALID-FASTAS.yaml"
    shell:
        """
        echo 'Making a Sequence Dictionary for {input} -> {output}'
        {params.executable} CreateSequenceDictionary -R "{input}" -O "{output}"
        """


rule SEQUENCE_INDEX:
    """
    Generate companion-file `.faidx` file for FATSA format sequence file
    """
    log:
        "results/{filename}.log",
    input:
        "results/{filename}.fa.gz",
    output:
        "results/{filename}.fa.gz.faidx",
    params:
        executable=config["Executables"]["SamTools"],
    conda:
        "envs/VALID-FASTAS.yaml"
    shell:
        """
        echo 'Making a Sequence Index for {input} -> {output}'
        {params.executable} faidx "{input}" -o "{output}"
        """
